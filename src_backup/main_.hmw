＊母艦セッティング
アプリケーション終了に、終了処理設定
エラー発生に、エラー処理設定
APPNAME=「Kobtype」
VER=「1.0.2」
'COPYLIGHT=「」
母艦は、「{APPNAME} {VER}」
母艦の、幅は、630
母艦の、リサイズは、いいえ
母艦のパス＆『data\』に、作業フォルダ変更


’使ったフリー素材のメモ用
使用素材は、「soundeffect-lab.info　decision9.mp3
tam-music.com tam-y12.mid」




＊起動時処理

’問題データ、ローマ字からひらがなに変換するデータを読み込む
「古文重要単語.csv」を、読む
問題データは、それ
「kana_roma_2.csv」を、読む
変換データは、それ


’セーブデータの確認　新規プレイヤーなら新しいデータを作る
「setting.txt」が、存在するか
もし、それが、いいえならば、（
「1
0
0
0
0」を、「setting.txt」へ、暗号化して保存
｛サウンドON-OFF、プレイ回数、キータイプ数、総プレイ時間、レーティング｝）

「tangospeed.kbtype」が、存在するか
もし、それが、いいえならば、（「」を、「tangospeed.kbtype」へ、暗号化して保存）


’セーブデータの読み込み
セーブ変数に、「setting.txt」を、暗号解除して読込。
単語入力速度に、「tangospeed.kbtype」を、暗号解除して読込。


’BGMをMCIでオープンする
「open tam-y12.mp3 type MPEGVideo alias kobtype」を、MCI送信
「open tamco02.mp3 type MPEGVideo alias kobtype2」を、MCI送信
「open tamco07.mp3 type MPEGVideo alias kobtype3」を、MCI送信


＊傾向チェック画面作成


傾向チェックパネルを、作る
その、レイアウトは、全体
その、可視は、いいえ

（0,0）へ、移動
習得数プログレスバーを、作る
その、幅は、640
習得数プログレスバーを、傾向チェックパネルへ、乗せる

（350,30）へ、移動
単語別情報ブラウザを、作る
その、幅は、280
その、高さは、370
単語別情報ブラウザを、傾向チェックパネルへ、乗せる


（10,30）へ、移動
傾向メモを、ブラウザとして、作成
その、幅は、320
その、高さは、317
傾向メモを、傾向チェックパネルへ、乗せる


傾向Cボタンを、作る
その、幅は、320
その、高さは、45
そは、「戻る」
その、イベントは、（
傾向チェックパネルの、可視は、いいえ
もし、セーブ変数＃０が、はいならば、（
’BGMフェード処理
Iを、1から、10まで、繰り返す（「setaudio kobtype volume to ｛I*100｝」を、MCI送信。「setaudio kobtype2 volume to ｛1000-I*100｝」を、MCI送信。0.02秒、待つ）
）
スタートパネルの、可視は、はい
ゲームパネルの、可視は、はい
）
傾向Cボタンを、傾向チェックパネルへ、乗せる




＊プレイ画面作成

ゲームパネルを、作る
その、レイアウトは、全体
その、可視は、いいえ
（10,10）へ、移動
情報ラベルを、作る
その、フォントサイズは、２０
そは、「」
情報ラベルを、ゲームパネルへ、乗せる


（10,80）へ、移動
問題ラベルを、作る
その、フォントサイズは、１６
問題ラベルを、ゲームパネルへ、乗せる
そは、「」

（200,130）へ、移動
答えラベルを、作る
その、フォントサイズは、12
その、フォント色は、赤色
そは、「」
答えラベルを、ゲームパネルへ、乗せる


（10,190）へ、移動
KBパネルを、作る
KBパネルを、ゲームパネルへ、乗せる
その、幅は、610
その、高さは、200

＊キーボード部分作成

文字サイズは、３０

’キーボード各段の配置レイアウト変数
KB作成変数は、「10,10,10
9,30,70
7,45,130」

KBマップは、「Q,W,E,R,T,Y,U,I,O,P
A,S,D,F,G,H,J,K,L
Z,X,C,V,B,N,M」
KBマップの、「,」を、「￥ｎ」に、置換。KB_アルファベット対応表は、それ
COUNT=0
Iを、0から、2まで、繰り返す（
KBマップ#Iの、「,」を、「￥ｎ」に、置換
それを、全角変換。配置用KBマップは、それ
Nを、1から、KB作成変数#0#Iまで、繰り返す（
一時変数は、N-1
「({KB作成変数#1#I+(一時変数)*60},{KB作成変数#2#I})に、移動。
Label{I}_{N}を、ボタンとして、作成
Label{I}_{N}を、KBパネルへ、乗せる」を、ひまわりする
’その、有効は、いいえ
そは、配置用KBマップ#一時変数
その、幅は、５０
その、高さは、５０
KB_アルファベット対応表#1#COUNTは、「Label{I}_{N}」
COUNT=COUNT+1
）
）
文字サイズは、１５
’KB_アルファベット対応表を、メモ記入

＊出題数選択画面作成
（115,50）へ、移動
出題数パネルを、作る
その、高さは、３００
その、幅は、４００
その、可視は、いいえ
出題数背景イメージを、作る
その、レイアウトは、全体
出題数背景イメージを、出題数パネルへ、乗せる
その、画像は、「back.png」
文字サイズは、１7
（128,50）へ、移動
出題数ラベルを、作る
そは、「出題数を選択」


（100,100）へ、移動
出題数Aボタンを、作る
その、幅は、200
その、高さは、40
そは、「通常プレイ」
その、イベントは、（出題数は、8
苦手克服は、いいえ
ゲームスタートに、飛ぶ）
出題数Bボタンを、作る
その、幅は、200
その、高さは、40
そは、「苦手克服」
その、イベントは、（
傾向データ作成
もし、苦手単語数が、4以下ならば、「このモードは苦手単語が5つ以上ある場合のみプレイできます。」と、言って、待機
出題数は、5
苦手克服は、はい
「苦手な単語を集中特訓します！」と、言う
ゲームスタートに、飛ぶ
）
出題数Cボタンを、作る
その、幅は、200
その、高さは、40
そは、「アタック」
その、イベントは、（出題数は、999999
苦手克服は、いいえ
「打ち間違えるまで続くモードです！」と、言う
ゲームスタートに、飛ぶ）

（10,265）へ、移動
文字サイズは、１５
出題数戻るボタンを、作る
そは、「戻る」
その、イベントは、（

出題数パネルの、可視は、いいえ
スタートパネルの、可視は、はい
）

出題数ラベルを、出題数パネルへ、乗せる
出題数戻るボタンを、出題数パネルへ、乗せる
出題数Aボタンを、出題数パネルへ、乗せる
出題数Bボタンを、出題数パネルへ、乗せる
出題数Cボタンを、出題数パネルへ、乗せる


ゲームパネルの、可視は、はい

＊タイトル画面作成
（115,50）へ、移動
スタートパネルを、作る
その、高さは、３００
その、幅は、４００

スタート背景イメージを、作る
その、レイアウトは、全体
スタート背景イメージを、スタートパネルへ、乗せる
その、画像は、「menu_back.png」

（10,280）へ、移動
スタートタイトルラベルを、作る
その、フォントサイズは、10
そは、VER'&" "&COPYLIGHT
スタートタイトルラベルを、スタートパネルへ、乗せる


（100,150）へ、移動
ゲームスタートボタンを、作る
その、幅は、200
その、高さは、40
その、イベントは、（
スタートパネルの、可視は、いいえ
出題数パネルの、可視は、はい
）

傾向チェックボタンを、作る
その、幅は、200
その、高さは、40
その、イベントは、（
傾向チェック読込
）


（310,275）へ、移動
サウンドチェックを、作る
その、フォントサイズは、10
その、幅は、75

もし、セーブ変数＃０が、はいならば、サウンドチェックの、値は、１
その、イベントは、（セーブ変数＃０は、（サウンドチェックの、値）。音楽処理イベント。）

’BGMの再生開始
音楽処理イベント


ゲームスタートボタンを、スタートパネルへ、乗せる
傾向チェックボタンを、スタートパネルへ、乗せる
サウンドチェックを、スタートパネルへ、乗せる



＊タイトル画面
キーボード無効処理
スタートパネルの、可視は、はい

母艦の、キー押した時は、（）

｛
’Enterキーで難易度選択画面へ
母艦の、キー押した時は、（もし、押されたキー=13ならば、（
スタートパネルの、可視は、いいえ
出題数パネルの、可視は、はい
出題数Aボタンに、注目。
’Escキーでメニューへ戻る
母艦の、キー押した時は、もし、押されたキー=27ならば、タイトル画面
））
｝
待機


＊キーボード消灯処理
’ソフトキーボードのハイライトを戻す
Iを、0から、2まで、繰り返す（
Nを、1から、KB作成変数#0#Iまで、繰り返す（
「Label{I}_{N}の、フォント色は、黒色」を、ひまわりする
）
）
戻る


＊キーボード無効処理
’ソフトキーボードを薄い表示にする
Iを、0から、2まで、繰り返す（
Nを、1から、KB作成変数#0#Iまで、繰り返す（
「Label{I}_{N}の、有効は、いいえ」を、ひまわりする
）
）
戻る

＊キーボード有効処理
’ソフトキーボードを普通の表示に戻す
Iを、0から、2まで、繰り返す（
Nを、1から、KB作成変数#0#Iまで、繰り返す（
「Label{I}_{N}の、有効は、はい」を、ひまわりする
）
）
戻る


＊ゲームスタート
傾向データ作成
母艦の、キー押した時は、（）

もし、セーブ変数＃０が、はいならば、「start.wav」を、演奏


出題数パネルの、可視は、いいえ

もし、セーブ変数＃０が、はいならば、（
「seek kobtype3 to 0」を、MCI送信
「play kobtype3 repeat」を、MCI送信
’フェード処理
Iを、1から、10まで、繰り返す（「setaudio kobtype volume to ｛600-I*100｝」を、MCI送信。「setaudio kobtype3 volume to ｛I*100｝」を、MCI送信。0.02秒、待つ）
）







キーボード有効処理


問題番号は、1
開始時間は、今

＊問題出題
情報ラベルは、「{問題番号}/{出題数}問目」
答え入力内容は、「」
もし、苦手克服が、はいならば、（
苦手単語の、行数
それの、乱数

出題データは、苦手単語＃それ
問題データから、出題データを、語句列挙。
それを、「,」で、区切る
出題番号は、それ＃０
）
違えば、（
問題データの、行数
それの、乱数
出題番号は、それ
出題データは、問題データ＃それ
）

元単語は、出題データ#1#0

単語は、元単語
’単語のローマ字変換
変換データを、反復（
それの、「,」を、「￥ｎ」に、置換
単語の、それ#0を、それ#1に、置換
単語は、それ
）

問題内容は、出題データ#0#0

'センタリング処理
問題内容の、バイト数
それは、INT(（56-それ）/2)
空白は、「」
それ回、空白は、空白&「 」

問題ラベルは、空白&問題内容&空白

問題プレイ時間は、システム時間


＊プレイ処理
キーボード消灯処理
もし、単語が、「」ならば、次の問題へ、飛ぶ

単語から、1文字、左端を切り取る
それを、大文字変換
キー単語は、それ
KB_アルファベット対応表から、それを、語句列挙
もし、それが、「」ならば、プレイ処理
それを、「,」で、区切る。それは、それ#0
「｛KB_アルファベット対応表#1#それ｝の、フォント色は、赤色」を、ひまわりする

母艦の、キー押した時は、（
文字(押されたキー）を、大文字変換
もし、それが、キー単語ならば、(
セーブ変数＃2は、セーブ変数＃2+1
キー単語を、小文字変換
それを、半角変換
答え入力内容は、答え入力内容&それ
’単語のローマ字変換
変換データを、反復（
それの、「,」を、「￥ｎ」に、置換
答え入力内容の、それ#1を、それ#0に、置換
答え入力内容は、それ
）
'センタリング処理
答え入力内容の、バイト数
それは、INT(（28-それ）/4)
空白は、「」
それ回、空白は、空白&「　」

答えラベルは、空白&答え入力内容&空白
プレイ処理
)
違えば、（
もし、出題数が、999999ならば、ゲーム終了
）
）
待機


＊次の問題

最新単語入力速度は、(システム時間-問題プレイ時間)/1000
一時変数は、単語入力速度＃出題番号
’デバッグ用　「｛出題番号｝　　｛一時変数｝　　　｛最新単語入力速度｝」を、言う。
それは、一時変数-最新単語入力速度。
もし、それが、0以上ならば、（単語入力速度＃出題番号は、最新単語入力速度。）
違えば、もし、一時変数が、0ならば、（単語入力速度＃出題番号は、最新単語入力速度。）

もし、セーブ変数＃０が、はいならば、「decision9.mp3」を、演奏
セーブ変数＃１=セーブ変数＃１+1
キーボード消灯処理

母艦の、キー押した時は、（）
’答えラベルの、フォント色は、緑色
0.5秒、待つ
’答えラベルの、フォント色は、赤色
答えラベルは、「」
もし、出題数が、問題番号ならば、ゲーム終了へ、飛ぶ
問題番号は、問題番号+1
問題出題
待機

＊ゲーム終了
今と、開始時間の、秒差。
セーブ変数＃３は、セーブ変数＃３+それ
「ゲーム終了！
｛それ｝秒かかりました。
累計｛セーブ変数＃１｝問 解きました！」と、言う

旧苦手単語数は、苦手単語数-1
傾向データ作成
もし、苦手単語数が、旧苦手単語数以下ならば、「苦手な単語を｛旧苦手単語数-苦手単語数+1｝個克服しました！」と、言う

情報ラベルは、「」
答えラベルは、「」
問題ラベルは、「」


もし、セーブ変数＃０が、はいならば、（

’フェード処理
Iを、1から、10まで、繰り返す（「setaudio kobtype volume to ｛I*100｝」を、MCI送信。「setaudio kobtype3 volume to ｛1000-I*100｝」を、MCI送信。0.02秒、待つ）
「stop kobtype3」を、MCI送信
）


タイトル画面へ、飛ぶ
待機



＊音楽処理イベント
’セーブ変数＃0はサウンドのON,OFF

もし、セーブ変数＃０が、はいならば、（
「play kobtype repeat」を、MCI送信
「setaudio kobtype2 volume to 0」を、MCI送信。
「play kobtype2 repeat」を、MCI送信

）

もし、セーブ変数＃０が、いいえならば、（
「stop kobtype」を、MCI送信
「stop kobtype2」を、MCI送信
「seek kobtype to 0」を、MCI送信
「seek kobtype2 to 0」を、MCI送信

）

戻る



＊傾向チェック読込

スタートパネルの、可視は、いいえ
ゲームパネルの、可視は、いいえ
もし、セーブ変数＃０が、はいならば、（
’フェード処理
Iを、1から、10まで、繰り返す（「setaudio kobtype volume to ｛1000-I*100｝」を、MCI送信。「setaudio kobtype2 volume to ｛I*100｝」を、MCI送信。0.02秒、待つ）
）

傾向データ作成

傾向チェックパネルの、可視は、はい

単語別情報ブラウザの、URLは、母艦のパス＆『data\tango.html』

問題データの、行数

習得数プログレスバーの、値は、INT(緑カウント/それ）
（緑カウント/それ）を、3位で、四捨五入。
習得数プログレスバーの、ヒントは、「｛それ｝%」

傾向メモデータは、「<b>習得単語の数：」＆『<font color="green">』＆「｛緑カウント｝</font>/｛それ｝<br>
解いた問題の数：｛セーブ変数＃１｝個<br>
総プレイ時間：｛セーブ変数＃３｝秒<br>
総入力文字数：｛セーブ変数＃２｝文字<br><br>

【苦手な単語が」&『<font color="red">』&「｛苦手単語数｝</font>個あります】</b><br>※マウスを置くと意味が表示されます<br><br>
」

苦手単語を、反復（
	単語元データは、それ
    それを、「,」で、区切る
    選択中の単語は、それ＃1
    選択中の単語の意味は、それ＃0
	傾向メモデータは、傾向メモデータ＆『<span title="』&選択中の単語の意味&『">』&選択中の単語&「</span>」&「<br>」＆「￥ｎ」）

傾向メモデータを、「keikou.html」へ、保存

傾向メモの、URLは、母艦のパス＆『data\keikou.html』

待機



＊傾向データ作成
’HTMLで表を作る
HTMLデータは、『<center>
<table border="1">
<tr>
	<th>単語</th>
	<th>入力速度</th>
</tr>』
I=0
緑カウントは、0
苦手単語は、「」
問題データを、反復（
	単語元データは、それ
	それを、「,」で、区切る
    選択中の単語は、それ＃1
    選択中の単語の意味は、それ＃0
    もし、単語入力速度#Iが、「」ならば、単語入力速度#Iは、「0.0」
    ’判定の設定
    選択中の単語の、文字数。緑判定は、それ*0.6。赤判定は、それ*1
    色部分変数は、『<tr>』
    ’タイピング速度で表の色分け
    もし、単語入力速度＃Iが、0.001以上ならば、（色部分変数は、『<tr bgcolor="lightgreen">』。緑カウントは、緑カウント＋１）
    もし、単語入力速度＃Iが、緑判定以上ならば、（色部分変数は、『<tr bgcolor="khaki">』。緑カウントは、緑カウント-１）
    もし、単語入力速度＃Iが、赤判定以上ならば、（色部分変数は、『<tr bgcolor="coral">』。苦手単語は、苦手単語＆単語元データ＆「￥ｎ」）
	
    HTMLデータは、HTMLデータ＆色部分変数＆「￥ｎ」
    
	HTMLデータは、HTMLデータ＆『<td><span title="』&選択中の単語の意味&『">』&選択中の単語&「</span>」&「</td>」＆「￥ｎ」
	HTMLデータは、HTMLデータ＆「<td>」&単語入力速度＃I&「</td>」＆「￥ｎ」    
	HTMLデータは、HTMLデータ＆『</tr>』＆「￥ｎ」
    
    I=I+1
）

HTMLデータは、HTMLデータ＆『</table></center>』＆「￥ｎ」

HTMLデータを、「tango.html」へ、保存


苦手単語の、行数。
苦手単語数は、それ

戻る


＊アプリケーション終了

セーブ変数を、「setting.txt」へ、暗号化して保存
単語入力速度を、「tangospeed.kbtype」へ、暗号化して保存
単語入力速度を、「tango.txt」へ、保存
おわり


待機

＊エラー発生
「エラーが発生しました。プログラムを終了します。
エラーコードは｛エラー行｝です。」と、言う

アプリケーション終了